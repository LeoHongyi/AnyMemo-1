apply plugin: 'com.android.application'

repositories {
    jcenter()
    google()
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        versionCode 228
        versionName "10.10.3"
        minSdkVersion 16
        targetSdkVersion 27
        testApplicationId "org.liberty.android.fantastischmemo.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        multiDexEnabled true
        archivesBaseName = "AnyMemo"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            versionNameSuffix "-${getBuildString()}"
        }
        release {
            minifyEnabled true
            proguardFile 'proguard.cfg'
        }
    }

    flavorDimensions "mode", "api"

    productFlavors {
        free {
            applicationId "org.liberty.android.fantastischmemo"
            dimension "mode"
        }
        pro {
            applicationId "org.liberty.android.fantastischmemopro"
            dimension "mode"
        }
        dev {
            applicationId "org.liberty.android.fantastischmemodev"
            dimension "mode"
        }
        api21 {
            minSdkVersion 21
            dimension "api"
        }
        api16 {
            minSdkVersion 16
            dimension "api"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }
}

dependencies {
    implementation "com.android.support:support-core-utils:$rootProject.ext.androidSupportLibraryVersion"
    implementation "com.android.support:support-fragment:$rootProject.ext.androidSupportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.ext.androidSupportLibraryVersion"
    implementation "com.android.support:design:$rootProject.ext.androidSupportLibraryVersion"
    implementation "com.android.support:support-annotations:$rootProject.ext.androidSupportLibraryVersion"
    implementation "com.android.support:customtabs:$rootProject.ext.androidSupportLibraryVersion"
    implementation "com.android.support:multidex:$rootProject.ext.supportMultidexVersion"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.ext.supportConstraintLayoutVersion"

    implementation "com.j256.ormlite:ormlite-core:$rootProject.ext.ormliteVersion"
    implementation "com.j256.ormlite:ormlite-android:$rootProject.ext.ormliteVersion"
    implementation "com.squareup.okhttp3:okhttp:3.9.1"
    implementation "org.greenrobot:eventbus:3.0.0"

    implementation "io.reactivex.rxjava2:rxjava:2.0.4"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"

    implementation "commons-io:commons-io:2.4"

    implementation "com.google.guava:guava:23.4-android"
    implementation "com.opencsv:opencsv:3.4"
    implementation "com.github.PhilJay:MPAndroidChart:v2.1.6"
    implementation "com.rarepebble:colorpicker:2.0.0"
    implementation "com.simplify:ink:1.0.0"
    implementation "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
    implementation "com.google.android.gms:play-services-auth:12.0.1"
    compileOnly "javax.annotation:jsr250-api:1.0"

    androidTestImplementation "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-mockito:1.4"
    androidTestImplementation "org.mockito:mockito-core:1.10.19"
    androidTestImplementation "com.android.support:support-annotations:$rootProject.ext.androidSupportLibraryVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.ext.espressoVersion") {
        exclude group: "com.google.code.findbugs"
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-intents:$rootProject.ext.espressoVersion") {
        exclude group: "com.google.code.findbugs"
    }

    androidTestImplementation "com.android.support.test:runner:1.0.1"

    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
}

static def getBuildString () {
    def date = new Date()
    def formattedDate = date.format('yyMMdd')
    return "b" + formattedDate
}

task copySecretsIfNotExists << {
    if (file('src/main/java/org/liberty/android/fantastischmemo/AMSecrets.java').exists()) {
        logger.warn('Removing AMSecrets.java since it is now in common package.')
        delete 'src/main/java/org/liberty/android/fantastischmemo/AMSecrets.java'
    }
    if (!file('src/main/java/org/liberty/android/fantastischmemo/common/AMSecrets.java').exists()) {
        logger.warn('Empty AMSecrets.java is created from AMSecrets.java.template. Please fill in the values.')
        copy {
            from "src/main/java/org/liberty/android/fantastischmemo/common/"
            into "src/main/java/org/liberty/android/fantastischmemo/common/"
            rename "AMSecrets.java.template", "AMSecrets.java"
            include "AMSecrets.java.template"
        }
    }
}

preBuild.dependsOn copySecretsIfNotExists 
